#format IAC  %a 255
#format DONT %a 254
#format DO   %a 253
#format WONT %a 252
#format WILL %a 251
#format SB   %a 250
#format SE   %a 240

#format MSDP %a  69

#format VAR  %a  01
#format VAL  %a  02

#var REPORTABLE_VARIABLES {{HEALTH};{HEALTH_MAX};{MANA};{MANA_MAX};{MOVEMENT};{MOVEMENT_MAX};{EXP_TNL};{EXP_TNL_MAX};{MONEY};{EXITS};{WORLD_TIME}}

#nop Turn telnet debug off once everything works as it's spammy.

#config {debug telnet} {on}

#event {IAC WILL MSDP}
{
        #send {$IAC$DO$MSDP\};
MSDP_REPORT;
MSDP_CONFIGURE sound,1
}

#event {IAC SB MSDP}
{
        #nop Save all variables with an MSDP_ prefix.;
        #var {MSDP_%0} {%1};
#switch {"%0"}
{
#case {"PLAY_SOUND"}
{
play %1
};
#case {"NAME"} {INITIALIZE_SETTINGS}
}
}

#nop This event is generated at the end of an MSDP sub negotiation.

#event {IAC SB MSDP IAC SE}
{
        #nop
}

#nop Ask the server to report all keys inside REPORTABLE_VARIABLES

#alias {MSDP_REPORT}
{
        #var result {$IAC$SB$MSDP${VAR}REPORT};

        #foreach {$REPORTABLE_VARIABLES[]} {MSDP_TMP}
        {
                #var result {$result${VAL}$MSDP_TMP}
        };

        #send {$result$IAC$SE\}
}

#alias {MSDP_LIST}
{
        #send {$IAC$SB$MSDP${VAR}LIST${VAL}%0$IAC$SE\}
}

#alias {MSDP_SEND}
{
        #send {$IAC$SB$MSDP${VAR}SEND${VAL}%0$IAC$SE\}
}

#alias {MSDP_CONFIGURE %1,%2} {
#list {MSDP_VALUES} {create} {%2};
#var {index} {1};
#send {$IAC$SB$MSDP\};
#forall {%1} {
#send {${VAR}&0${VAL}${MSDP_VALUES[$index]}\};
#math {index} {$index+1}
};
#send {$IAC$SE\}
}

